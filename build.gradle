
plugins {
    id 'maven-publish'
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'signing'

group = 'com.lostsidewalk.newsgears'
version = '0.5'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(19)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'com.lostsidewalk.newsgears:newsgears-data:0.5.0'

    // SLF4J
    implementation 'org.slf4j:slf4j-api:2.0.5'

    // json
    implementation 'org.json:json:20230227'
    // spring-boot-starter
    implementation 'org.springframework.boot:spring-boot-starter:3.0.4'
    // commons-collections4
    implementation 'org.apache.commons:commons-collections4:4.4'
    // commons-lang
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    // commons-io
    implementation 'commons-io:commons-io:2.11.0'
    // jaxb-api
    implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
    // GSON
    implementation 'com.google.code.gson:gson:2.10.1'

    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'newsgears-json-publisher'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'newsgears-json-publisher'
                description = 'This module contains the FeedGears RSS platform publisher component.'
                url = 'https://www.lostsidewalk.com/dev/newsgears-json-publisher'
                licenses {
                    license {
                        name = 'GNU General Public License'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'elderscrollsjesus'
                        name = 'Michael Harris'
                        email = 'meh@lostsidewalk.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/lostsidewalk/newsgears-json-publisher.git'
                    developerConnection = 'scm:git:https://github.com/lostsidewalk/newsgears-json-publisher.git'
                    url = 'https://www.lostsidewalk.com/dev/newsgears-json-publisher'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/releases'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
